PROGRAM MAIN
  USE KEYS, ONLY: ACTION,NTRIALS,NPART,NMT,READLENS,MTLENFILE,RANDNUC,READNUC,MTNUCFILE,TOTMTLEN
  USE PARTICLEUTIL, ONLY: PARTICLEGROUP,PARAMLIST,CLEANUPPARTICLEGROUP,SETUPPARAMS,CLEANUPPARAMS

  IMPLICIT NONE
  DOUBLE PRECISION :: T1,T2
  TYPE(PARTICLEGROUP), POINTER :: GROUPLIST(:)
  TYPE(PARTICLEGROUP), POINTER :: PGROUP
  TYPE(PARAMLIST), TARGET :: PARAMS
  TYPE(PARAMLIST),POINTER :: PARAMP

  INTEGER :: TC

  ! start timer
  CALL CPU_TIME(T1)

  !parses free format input file
  CALL READKEY
  PARAMP=>PARAMS
  CALL SETUPPARAMS(PARAMP)

  !initialize particle arrays
  ALLOCATE(GROUPLIST(NTRIALS))

  SELECT CASE(ACTION)
    CASE('TRANSPORTSIM')
      PARAMP%STARTSTATE = 1 ! start on microtubules
      CALL TRANSPORTSIMDRIVER(GROUPLIST,NPART,NMT,PARAMP,RANDNUC=RANDNUC,READLENS=READLENS,&
                            MTLENFILE=MTLENFILE,READNUC=READNUC,MTNUCFILE=MTNUCFILE)
    CASE('PARTCAPTURE')
      PARAMP%STARTSTATE = 2 ! start at domain end
      CALL TRANSPORTSIMDRIVER(GROUPLIST,NPART,NMT,PARAMP,RANDNUC=RANDNUC,READLENS=READLENS,&
                      MTLENFILE=MTLENFILE,READNUC=READNUC,MTNUCFILE=MTNUCFILE,TOTMTLEN=TOTMTLEN)
    CASE('PARTCAPTURE_UNIF')
      PARAMP%STARTSTATE = 3 ! start uniformly distributed
      CALL TRANSPORTSIMDRIVER(GROUPLIST,NPART,NMT,PARAMP,RANDNUC=RANDNUC,READLENS=READLENS,&
                      MTLENFILE=MTLENFILE,READNUC=READNUC,MTNUCFILE=MTNUCFILE,TOTMTLEN=TOTMTLEN)                      
    CASE DEFAULT
    PRINT*, 'ERROR IN MAIN: unidentified action', ACTION
    STOP 1
  END SELECT

  !clean up arrays and deallocate memory
  DO TC = 1,NTRIALS
    PGROUP=>GROUPLIST(TC)
    CALL CLEANUPPARTICLEGROUP(PGROUP)
  END DO
  DEALLOCATE(GROUPLIST)

  CALL CPU_TIME(T2)
  PRINT*,''
  PRINT '(A8,F12.3,A1)','Runtime:', T2-T1,'s'
  WRITE(*,*), ACHAR(7)

  CONTAINS  
    SUBROUTINE TRANSPORTSIMDRIVER(GROUPLIST,NPART,NMT,PARAMP,RANDNUC,READLENS,MTLENFILE,&
                                  READNUC,MTNUCFILE,TOTMTLEN)
    ! run transport sims on dynamic microtubules
      USE KEYS, ONLY: NTRIALS
      USE GENUTIL, ONLY: GRND
      USE TRANSPORTUTIL, ONLY: TRANSPORTSIM
      USE PARTICLEUTIL, ONLY: PARTICLEGROUP,PARAMLIST,SETUPPARTICLEGROUP,READMTLENS,READMTNUC

      IMPLICIT NONE
      TYPE(PARTICLEGROUP),TARGET :: GROUPLIST(NTRIALS)
      TYPE(PARTICLEGROUP),POINTER :: PGROUP
      TYPE(PARAMLIST),POINTER :: PARAMP
      INTEGER,INTENT(IN) :: NPART,NMT
      LOGICAL,INTENT(IN) :: RANDNUC
      LOGICAL,INTENT(IN),OPTIONAL :: READLENS,READNUC
      DOUBLE PRECISION, ALLOCATABLE :: MTLENS(:,:),MTNUC(:,:)
      CHARACTER(LEN=*),INTENT(IN),OPTIONAL :: MTLENFILE,MTNUCFILE
      DOUBLE PRECISION,INTENT(IN),OPTIONAL :: TOTMTLEN
      INTEGER :: TC,NC,MC

      ALLOCATE(MTLENS(NMT,NTRIALS))
      ALLOCATE(MTNUC(NMT,NTRIALS))

      MTLENS = -1D0
      MTNUC = 0D0

      IF(PRESENT(READLENS)) THEN
        IF(READLENS) THEN
          CALL READMTLENS(MTLENS,MTLENFILE,NTRIALS,NMT)
        ELSEIF(PRESENT(TOTMTLEN)) THEN
          MTLENS = TOTMTLEN/NMT
        END IF
      END IF
      
      IF(PRESENT(READNUC)) THEN
        IF(READNUC) THEN
          CALL READMTNUC(MTNUC,MTNUCFILE,NTRIALS,NMT)
        END IF
      END IF

      DO TC = 1,NTRIALS
        PGROUP=>GROUPLIST(TC)
        CALL SETUPPARTICLEGROUP(PGROUP,NPART,NMT,PARAMP,MTLENS=MTLENS(:,TC),RANDNUC=RANDNUC,&
                                MTNUC=MTNUC(:,TC))
      END DO
      
      CALL TRANSPORTSIM(GROUPLIST,PARAMP,NTRIALS)
      
    END SUBROUTINE TRANSPORTSIMDRIVER

END PROGRAM MAIN